require conf/machine/am57xx-evm.conf

MACHINEOVERRIDES .= ":am57xx-evm"

# Define specific familly common machine name
MACHINEOVERRIDES .= ":ledgecommon"

PREFERRED_PROVIDER_virtual/kernel = "linux-ledge"
PREFERRED_VERSION_linux-ledge = "mainline%"
INITRAMFS_IMAGE = "ledge-initramfs"
WKS_FILE_DEPENDS += "ledge-initramfs"
do_image_wic[depends] += "ledge-initramfs:do_image_complete"

# Hack for GPU: do not use imx-gpu-viv
PREFERRED_PROVIDER_virtual/egl_ledge-ti-am572x = "mesa"
PREFERRED_PROVIDER_virtual/libgles1_ledge-ti-am572x = "mesa"
PREFERRED_PROVIDER_virtual/libgles2_ledge-ti-am572x = "mesa"
PREFERRED_PROVIDER_virtual/mesa_ledge-ti-am572x = "mesa"
PREFERRED_PROVIDER_virtual/libgl_ledge-ti-am572x = "mesa"

PACKAGECONFIG_pn_mesa = "${@bb.utils.filter('DISTRO_FEATURES', 'wayland ', d)} \
                   ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'opengl egl gles gbm dri', '', d)} \
                   ${@bb.utils.contains('DISTRO_FEATURES', 'x11 opengl', 'x11', '', d)} \
                   ${@bb.utils.contains('DISTRO_FEATURES', 'x11 vulkan', 'dri3', '', d)} \
                   \
                   gallium \
        "

KERNEL_DEVICETREE = "am572x-idk.dtb"

PREFERRED_PROVIDER_virtual/bootloader = "u-boot-ledge"
PREFERRED_PROVIDER_u-boot = "u-boot-ledge"
unset UBOOT_MACHINE
UBOOT_CONFIG = "trusted"
UBOOT_CONFIG[trusted] = "am57xx_evm_defconfig,,u-boot.img"
EXTRA_IMAGEDEPENDS_remove = "u-boot"

MACHINE_EXTRA_RRECOMMENDS = "prueth-fw optee-os"
MACHINE_FEATURES += "optee"
MACHINE_FEATURES += "tsn"

MACHINE_EXTRA_RDEPENDS += " \
    edk2-efi-prebuild-fw \
    "

RDEPENDS_${KERNEL_PACKAGE_NAME}-base_append_ledge-ti-am572x = " prueth-fw"
FILES_${KERNEL_PACKAGE_NAME}-devicetree_append_ledge-ti-am572x += "/${KERNEL_IMAGEDEST}/*.itb"

KERNEL_IMAGETYPES = "zImage"

# bootfs images
IMAGE_CLASSES += " image_types-ledgebootfs"
IMAGE_FSTYPES += " ledgebootfs "

# WIC
IMAGE_BOOT_FILES ?= "u-boot.img MLO"
IMAGE_FSTYPES_remove += "tar.bz2 tar.xz ext4"
IMAGE_FSTYPES += "wic"
WKS_FILE += "ledge-ti-am572x.wks.in"
WIC_CREATE_EXTRA_ARGS += "--no-fstab-update"

# for image generated on ext4, force UUID
EXTRA_IMAGECMD_ext4 = "-U 6091b3a4-ce08-3020-93a6-f755a22ef03b"
